plugins {
  id 'com.github.ben-manes.versions' version '0.28.0'
  id 'me.champeau.gradle.jmh' version '0.5.0'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    group = 'me.champeau.sample'
    version = '1.0-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        jcenter()
    }

    dependencies {
        testCompile 'org.openjdk.jmh:jmh-core:1.23'
        testCompile 'org.openjdk.jmh:jmh-generator-annprocess:1.23'
    }


    dependencyUpdates.resolutionStrategy {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview', 'b', 'pr', 'ea'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-+]*/
                }
                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }
}


jmh {
    include = 'org\\.openjdk\\.jmh\\.samples\\.JMHSample_01_HelloWorld.*'
    humanOutputFile = null
    warmupIterations = 1
    iterations = 1
}

// it is possible to tweak the jmhJar task directly
jmhJar.baseName = 'custom'

subprojects {
    apply plugin: 'me.champeau.gradle.jmh'

    jmh {
        include = 'org\\.openjdk\\.jmh\\.samples\\.JMHSample_01_HelloWorld.*'
        humanOutputFile = null
        warmupIterations = 1
        iterations = 1
    }

}
